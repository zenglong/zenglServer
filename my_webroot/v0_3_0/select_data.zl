use builtin;
use mysql;
inc 'config.zl';

fun finish_with_error(con)
	err = mysqlError(con);
	mysqlClose(con);
	print '<p style="color:red">' + err + '</p></body></html>';
	bltExit(err);
endfun

fun mysql_query(con, sql)
	if(mysqlQuery(con, sql))
		finish_with_error(con);
	endif
	result = mysqlStoreResult(con);
	return_array = bltArray();
	while(mysqlFetchResultRow(result, &result_array))
		return_array[] = result_array;
	endwhile
	mysqlFreeResult(result);
	return return_array;
endfun

print '<!Doctype html>
<html>
<head><meta http-equiv="content-type" content="text/html;charset=utf-8" />
<title>浏览cars测试表数据</title>
</head>
<body>';

con = mysqlInit();
if(!con)
	bltExit('mysqlInit failed');
endif

if(!mysqlRealConnect(con, config['db_host'], config['db_user'], config['db_passwd'], config['db_name'], config['db_port']))
	finish_with_error(con);
endif

if(mysqlSetCharacterSet(con, "utf8"))
	finish_with_error(con);
endif
print 'mysql设置字符集:' + mysqlCharacterSetName(con)+ '<br/>';

cars_array = mysql_query(con, "select * from cars limit 10");
print 'cars测试表中的前10条数据：<br/>';
for(i=0;bltIterArray(cars_array,&i,&car);)
	print '<p>' + car['id'] + ': ' + car['name'] + ' (价格: ' + car['price'] + ')<br/>' + 
			'[' + car['description'] + ']</p>';
endfor

if(!i) print '<p>暂无数据！</p>'; endif

mysqlClose(con);
print '关闭mysql连接!'+ 
		'<br/><br/>
		<a href="insert_data.zl">点击我，插入数据</a>';

print '</body></html>';
